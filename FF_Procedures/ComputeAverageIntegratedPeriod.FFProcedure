// ComputeAverageIntegratedPeriod()
// Computes spacecraft's averaged orbital period (averaged over the user-specified interval)
// the period is calculated as the integrated period from ascending node to ascending node
//
// Inputs / Outputs (see below)
//
// 
// Dependencies:  
//       - None
//
// Assumptions:
//       - Assumes a closed orbit with a well-defined line of nodes
//       


Define Procedure ComputeAverageIntegratedPeriod(	Spacecraft            sc,	/* Input/       - The configured spacecraft for average integrated period is to be computed */
													Variable  useSaveRestore,   /* Input/       - flag specifying whether or not to save/Restore the spacecraf (1=yes, 0=no)  */
													Variable      nRevsToAvg,	/* Input/       - The number of orbits over which to average the integrated period  */
													Variable    avgIntPeriod);	/*      /Output - the average integrated period of the spacecraft in seconds */
	
	Variable i;
	TimeSpan revStart;
	
	Array periodAtNode;
	
	periodAtNode.Clear();
	periodAtNode.Dimension = nRevsToAvg;
	
	If (useSaveRestore);
		sc.Save();
	End;
	
	sc.StepToNode(0);
	
	// build up array of integrated periods between nodes
	For i = 0 to nRevsToAvg-1;

		revStart = sc.Epoch;
		sc.StepToNode(0);
		periodAtNode[i] = (sc.Epoch - revStart).ToSeconds();
	End;
	
	// compute the mean
	avgIntPeriod = periodAtNode.Mean();
	
	If (useSaveRestore);
		sc.Restore();
	End;
	
EndProcedure;