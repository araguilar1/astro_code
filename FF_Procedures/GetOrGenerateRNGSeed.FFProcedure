
// GetOrGenerateRNGSeed()
// This procedure either gets a RNG seed value from file, if it exists, or if it does not exist, generates a new one and
// archives it to file.  The RNG seed value filename will be path+/rngSeed.xml, and will be a FreeFlyer object file.
//
// This procedure sets the RNG seed value in two places.  First is for the provided RandomNumberGenerater, and second is
// for the global (under the hood) random number generator object that FreeFlyer functions (like GaussianNoise()) use.
//
// Inputs / Outputs (see below)
//
// Dependencies:
//       - None
//
// Assumptions:
//       - assumes input path exists on the file system
//       - assumes the RNG seed file has the name 'rngSeed.xml'

Define Procedure GetOrGenerateRNGSeed(String                path,   /* Input/       - full or relative path to directory where seed file may be contained ( including trailing '/') */
									  RandomNumberGenerator  rng,   /* Input/Output - RNG to seed */
									  Variable         seedValue);  /*      /Output - RNG Seed value that is obtained or generated */
	
	String seedName;
	
	
	seedName = path+"rngSeed.xml";
	
	If (FileSystem.Exists( seedName));
		Get seedValue from seedName;
	Else;
		seedValue = rng.IntegerUniformDistribution(1, 99999);

		Put seedValue to seedName;
	End;

	rng.Seed = seedValue;
	SeedNoise( seedValue);
	
EndProcedure;