


// DownloadTLEs()
// Downloads TLE data from a remote URL using an HTTP request
//
// Inputs/Outputs:  (See below)
//        Example:  remoteHostURL = "www.celestrak.com"
//                  requestStr    = "GET /NORAD/elements/supplemental/starlink.txt" + @" HTTP/1.0\n\n"
//
// Dependencies:
//       - None
//
// Assumptions:
//       - Assumes a port number of 80 on the remote host


Define Procedure DownloadTLEs(	String   remoteHostURL, 	/* Input/       - URL or IP address of remote server  */
								String      requestStr, 	/* Input/       - a valid request string of the remote server */
								StringArray    tleData);	/*      /Output - all the requested TLE data  */

	Variable i;
	Variable errCnt;
	Variable dlStatus;
	
	String dataString;
	String hdr;
	
	Socket remoteHost;
	
	
	tleData.Clear();
	
	// set up socket
	remoteHost.IPAddress  = remoteHostURL;
	remoteHost.PortNumber = 80;
	remoteHost.SocketTimeout = 120;
	remoteHost.SocketType    = "client";
	remoteHost.SocketIsAscii = 1;
	remoteHost.ErrorAction   = "Ignore";

	Try sending ErrorCount to errCnt;
		Open remoteHost;
	End;

	If (errCnt > 0);
		Diagnostics.ReportErrorMessage(0, "Error connecting to "+remoteHostURL+" server.  You may not have a live internet connection, or the server may be down.");
	End;

	Send requestStr to remoteHost;


	While (dlStatus == 0);

		Receive dataString from remoteHost;
		tleData.PushBack(dataString);
		dlStatus = remoteHost.Status;
	End;	

	Close remoteHost;
	
	// Remove HTTP Headers
	For i = 0 to 8;
		hdr = tleData.PopFront;
	End;
	
	// remove any duplicate lines
	i = 1;
	While (i < tleData.Dimension);
		
		If (tleData[i] == tleData[i-1]);
			tleData.Erase(i);
		Else;
			i++;
		End;
	End;

EndProcedure;