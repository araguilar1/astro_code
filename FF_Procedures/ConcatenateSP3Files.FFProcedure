
// ConcatenateSP3Files()
// Reads multiple SP3 ephemeris files, and concatenates them into one contiguous file
//
// Inputs / Outputs (see below)
//
// Dependencies:  (None)
//
// Assumptions:
//       - assumes all SP3 Ephemeris files exist at the specified base path
//   

Define Procedure ConcatenateSP3Files(String         basePath,     /* Input/       - full or relative path to SP3 files (including trailing '/')   */
									 StringArray     inFiles,     /* Input/       - Array of filenames of SP3 files to be read   */ 
									 String      outFileName);    /* Input/       - Output filename with full or relative path  */ 
	
	StringArray outfile[0];
	StringArray file[0];
	StringArray check[0];

	Variable status;
	Variable nFiles;
	Variable nTotal;
	Variable nLines;
	Variable nHdr;
	Variable i;
	Variable j;
	Variable start;
	Variable end;
	Variable diff;
	
	file.ClearArray();
	check.ClearArray();
	outfile.ClearArray();
	
	nFiles = inFiles.Dimension;
	
	For i = 0 to nFiles-1;

		nLines = file.ReadFromFile(basePath+inFiles[i]);
		// purge eof line
		nLines = file.Erase(nLines-1);

		If (i == 0);
			end = 0;		
		Else;
			end    = outfile.Dimension;
			status = 0;

			// purge header
			While(status == 0);
				file[0].Split(" ", check);

				If (check[0] == "/*");
					nLines = file.Erase(0);
					nLines = file.Erase(0);
					nLines = file.Erase(0);
					status = 1;
				End;
				nLines = file.Erase(0);
			End;		
		End;

		nTotal = nTotal + nLines;
		outfile.Dimension = nTotal;


		For j = 0 to nLines-1;

			outfile[end+j] = file[j];
		End;
		file.ClearArray();

	End;

	// add eof line
	outfile.Dimension += 1;
	outfile[outfile.Dimension-1] = "EOF";

	outfile.WriteToFile(outFileName);
	
EndProcedure;