
// ComputeArgumentOfPerigeeRateJ2()
// Computes the drift rate of the orbit's Argument of Perigee as a function of 
// only Earth's J2 term
// Inputs:
//       - see below
//
// Dependencies:
//     - None
//
// Assumptions:
//     - Earth as central body
//     - AOP = 90
//     - uses Brouwer-Lyddane J2 elements

Define Procedure ComputeArgumentOfPerigeeRateJ2(Spacecraft      sc,    /* Input/       - Spacecraft to compute the AOP rate  */
												Variable   outRate);   /*      /Output - AOP rate, in degrees per day  */

    Variable meanMotion;
             meanMotion = sqrt(sc.CentralBodyObject.Mu / (sc.BLJ2A*sc.BLJ2A*sc.BLJ2A));
    Variable eSquared;
             eSquared   = sc.BLJ2E*sc.BLJ2E;
			
    Variable semiLatusRectum;
             semiLatusRectum = sc.BLJ2A * (1-eSquared);

    Variable Es;
             Es = (sc.CentralBodyObject.Radius / semiLatusRectum);

    Variable pi;
             pi = acos(-1);
    Variable sinOfInclinationSquared;
             sinOfInclinationSquared  = sin(rad(sc.BLJ2I));
             sinOfInclinationSquared *= sinOfInclinationSquared;

    Variable commonCalc;
             commonCalc = (3/2) * sc.CentralBodyObject.J2 * Es * Es;

    // Final values
    Variable anomalisticMeanMotion;
             anomalisticMeanMotion = meanMotion * (1 + commonCalc * (1 - (3/2) * sinOfInclinationSquared) * sqrt(1-eSquared));

    outRate = deg(commonCalc * anomalisticMeanMotion * (2 - (5/2)*sinOfInclinationSquared))*86400;

EndProcedure;

